plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
	id 'com.diffplug.spotless' version '6.8.0'
}

def compose_version = '1.2.0'
def appcompat_version = "1.6.0-rc01"
def nav_version = "2.5.2"

android {
	compileSdk 33
	defaultConfig {
		applicationId "com.github.paylike.sample"
		minSdk 26
		targetSdk 33
		versionCode 1
		versionName "1.0"
		vectorDrawables {
			useSupportLibrary true
		}
	}
	buildTypes {
		def localProperties = new Properties()

		debug {
			localProperties.load(new FileInputStream(rootProject.file("local.properties")))
			buildConfigField(
					"String",
					"PaylikeMerchantApiKey",
					(localProperties.containsKey("PaylikeMerchantApiKey")
							? localProperties.getProperty("PaylikeMerchantApiKey")
							: "new String()"
					)
			)
		}
		release {
			localProperties.load(new FileInputStream(rootProject.file("local.properties")))
			buildConfigField(
					"String",
					"PaylikeMerchantApiKey",
					(localProperties.containsKey("PaylikeMerchantApiKey")
							? localProperties.getProperty("PaylikeMerchantApiKey")
							: "new String()"
					)
			)
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = '1.8'
	}
	buildFeatures {
		viewBinding true
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion compose_version
	}
	packagingOptions {
		exclude 'META-INF/DEPENDENCIES'
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
	}
}

dependencies {
	debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
	implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
	implementation "androidx.navigation:navigation-compose:$nav_version"
	implementation "androidx.appcompat:appcompat:$appcompat_version"

	implementation project(':kotlin_sdk')
}

spotless {
	kotlin {
		target 'src/**/*.kt', 'src/**/*.kts'
		ktfmt().kotlinlangStyle()
		trimTrailingWhitespace()
		endWithNewline()
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
assemble.dependsOn 'spotlessApply'
